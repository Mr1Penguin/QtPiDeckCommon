cmake_minimum_required (VERSION 3.15)

project("QtPiDeckCommon" LANGUAGES CXX)

option(VISUAL_STUDIO "" OFF)

include(CTest)

if(MSVC AND CMAKE_CXX_COMPILER_VERSION VERSION_GREATER_EQUAL 19.14)
    set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} /experimental:external /external:W0 ")
    set(CMAKE_INCLUDE_SYSTEM_FLAG_CXX "/external:I ")
endif()

function(set_warnings_flags target)
    if(MSVC)
        if (CMAKE_CXX_COMPILER_VERSION VERSION_GREATER_EQUAL 19.14)
            target_compile_options(${target} PRIVATE /W4 /WX)
        endif()
    else()
        target_compile_options(${target} PRIVATE -Wall -Wextra -pedantic -Werror)
    endif()
endfunction()

if(CMAKE_SOURCE_DIR STREQUAL CMAKE_CURRENT_SOURCE_DIR)
    set(CMAKE_AUTOUIC ON)
    set(CMAKE_AUTOMOC ON)
    set(CMAKE_AUTORCC ON)
    option(FORCE_CPP17 "" OFF)
    list(FIND CMAKE_CXX_COMPILE_FEATURES "cxx_std_20" CXX20_POS)
    if (FORCE_CPP17 OR ${CXX20_POS} EQUAL -1)
        set(CMAKE_CXX_STANDARD 17)
    else() 
        set(CMAKE_CXX_STANDARD 20)
    endif()
    unset(CXX20_POS)
    set(CMAKE_CXX_STANDARD_REQUIRED ON)
endif()

option(BUILD_COMMON_TESTS "" ON)
option(CODECOV "" OFF)

add_subdirectory(QtPiDeckCommon)
add_subdirectory(QtChecker)
add_subdirectory(QtDefinitions)
add_subdirectory(Tests)